#!/opt/perl/bin/perl

use 5.028;

use strict;
use warnings;
no  warnings 'syntax';

use experimental 'signatures';
use experimental 'lexical_subs';

use lib "../lib";

use Getopt::Long;

use Chess::Infinite;

my %graph_options;

GetOptions  "piece=s"             =>  \(my $piece_name       = "knight"),
            "board=s"             =>  \(my $board_name       = "spiral"),
            "start=i"             =>  \(my $start            =       1),
            "max_moves=i"         =>  \(my $max_moves        =  10_000),
            "max_bounding_box=i"  =>  \(my $max_bounding_box =     200),
            "heading=s"           =>  \ my $heading,

            #
            # Controls what will be drawn
            #
            "show-path!"          =>  \($graph_options {show_path}      = 1),
            "show-visited!"       =>  \($graph_options {show_visited}   = 1),
            "show-unvisited!"     =>  \($graph_options {show_unvisited} = 1),
            "show-terminals!"     =>  \($graph_options {show_terminals} = 1),

            #
            # Scale/margin options
            #
            "margin-left=i"       =>  \($graph_options {margin_left}),
            "margin-top=i"        =>  \($graph_options {margin_top}),
            "margin-right=i"      =>  \($graph_options {margin_right}),
            "margin-bottom=i"     =>  \($graph_options {margin_bottom}),
            "scale=i"             =>  \($graph_options {scale}),

;

$board_name = fc $board_name;

my %board2class = (
    fc spiral      =>  "Spiral",
    fc triangle    =>  "Triangle",
    fc square      =>  "Square",
);


die "'$board_name': no such board\n" unless    $board2class {$board_name};

my $board_class = "Chess::Infinite::Board::" . $board2class {$board_name};

my $board = $board_class -> new -> init;
my $piece = piece $piece_name, board    =>  $board,
                               heading  =>  $heading
            or die "'$piece_name': No such piece\n";

$piece -> run (max_moves         =>  $max_moves,
               max_bounding_box  =>  $max_bounding_box,
               start             =>  $start,);

say $piece -> summary;


Chess::Infinite::Grapher:: -> route (piece => $piece,
                                     %graph_options);

Chess::Infinite::Grapher:: -> movement (piece => $piece,
                                        %graph_options);


__END__
